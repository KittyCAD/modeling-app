// A mounting bracket for the Focusrite Scarlett Solo audio interface
// This is a bracket that holds an audio device underneath a desk or shelf. The audio device has dimensions of 144mm wide, 80mm length and 45mm depth with fillets of 6mm. This mounting bracket is designed to be 3D printed with PLA material


// define constants in mm
const radius = 6.0
const width = 144.0
const length = 80.0
const depth = 45.0
const thk = 4
const holeDiam = 5
const tabLength = 25
const tabWidth = 12
const tabThk = 4

// define a rectangular shape func
fn rectShape = (pos, w, l) => {
  const rr = startSketchOn('xy')
    |> startProfileAt([pos[0] - (w / 2), pos[1] - (l / 2)], %)
    |> line(endAbsolute = [pos[0] + w / 2, pos[1] - (l / 2)], tag = $edge01)
    |> line(endAbsolute = [pos[0] + w / 2, pos[1] + l / 2], tag = $edge02)
    |> line(endAbsolute = [pos[0] - (w / 2), pos[1] + l / 2], tag = $edge03)
    |> close(tag = $edge04)
  return rr
}

// define the bracket plane
const bracketPlane = {
  plane: {
    origin: { x: 0, y: length / 2 + thk, z: 0 },
    xAxis: { x: 1, y: 0, z: 0 },
    yAxis: { x: 0, y: 0, z: 1 },
    zAxis: { x: 0, y: -1, z: 0 }
  }
}

// build the bracket sketch around the body
fn bracketSketch = (w, d, t) => {
  const s = startSketchOn(bracketPlane)
    |> startProfileAt([-w / 2 - t, d + t], %)
    |> line(endAbsolute = [-w / 2 - t, -t], tag = $edge1)
    |> line(endAbsolute = [w / 2 + t, -t], tag = $edge2)
    |> line(endAbsolute = [w / 2 + t, d + t], tag = $edge3)
    |> line(endAbsolute = [w / 2, d + t], tag = $edge4)
    |> line(endAbsolute = [w / 2, 0], tag = $edge5)
    |> line(endAbsolute = [-w / 2, 0], tag = $edge6)
    |> line(endAbsolute = [-w / 2, d + t], tag = $edge7)
    |> close(tag = $edge8)
    |> extrude(length = length + 2 * thk)
  return s
}

// build the body of the bracket
const bs = bracketSketch(width, depth, thk)
const bracketBody = bs
  |> fillet(
       radius = radius,
       tags = [
         getPreviousAdjacentEdge(bs.sketch.tags.edge7),
         getPreviousAdjacentEdge(bs.sketch.tags.edge2),
         getPreviousAdjacentEdge(bs.sketch.tags.edge3),
         getPreviousAdjacentEdge(bs.sketch.tags.edge6)
       ]
     )


// sketch on the face
const retBack = startSketchOn(bs, bs.sketch.tags.edge4)
  |> startProfileAt([length / 2 + thk, 0], %)
  |> line(end = [0, thk])
  |> line(end = [-thk, 0])
  |> line(end = [0, -thk])
  |> close()
  |> extrude(length = width - 40)
