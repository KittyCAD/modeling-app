// A mounting bracket for the Focusrite Scarlett Solo audio interface
// This is a bracket that holds an audio device underneath a desk or shelf. The audio device has dimensions of 144mm wide, 80mm length and 45mm depth with fillets of 6mm. This mounting bracket is designed to be 3D printed with PLA material


// define constants in mm
const radius = 6.0
const width = 144.0
const length = 80.0
const depth = 45.0
const thk = 4
const holeDiam = 5
const tabLength = 25
const tabWidth = 12
const tabThk = 4

// define a rectangular shape func
fn rectShape = (pos, w, l) => {
  const rr = startSketchOn('xy')
    |> startProfileAt([pos[0] - (w / 2), pos[1] - (l / 2)], %)
    |> lineTo([pos[0] + w / 2, pos[1] - (l / 2)], %, $edge01)
    |> lineTo([pos[0] + w / 2, pos[1] + l / 2], %, $edge02)
    |> lineTo([pos[0] - (w / 2), pos[1] + l / 2], %, $edge03)
    |> close(%, $edge04)
  return rr
}

// define the bracket plane
const bracketPlane = {
  plane: {
    origin: { x: 0, y: length / 2 + thk, z: 0 },
    x_axis: { x: 1, y: 0, z: 0 },
    y_axis: { x: 0, y: 0, z: 1 },
    z_axis: { x: 0, y: -1, z: 0 }
  }
}

// build the bracket sketch around the body
fn bracketSketch = (w, d, t) => {
  const s = startSketchOn(bracketPlane)
    |> startProfileAt([-w / 2 - t, d + t], %)
    |> lineTo([-w / 2 - t, -t], %, $edge1)
    |> lineTo([w / 2 + t, -t], %, $edge2)
    |> lineTo([w / 2 + t, d + t], %, $edge3)
    |> lineTo([w / 2, d + t], %, $edge4)
    |> lineTo([w / 2, 0], %, $edge5)
    |> lineTo([-w / 2, 0], %, $edge6)
    |> lineTo([-w / 2, d + t], %, $edge7)
    |> close(%, $edge8)
  return s
}

// build the body of the bracket
const bs = bracketSketch(width, depth, thk)
const bracketBody = bs
  |> extrude(length + 2 * thk, %)
  |> fillet({
       radius: radius,
       tags: [
         getPreviousAdjacentEdge(bs.tags.edge7),
         getPreviousAdjacentEdge(bs.tags.edge2),
         getPreviousAdjacentEdge(bs.tags.edge3),
         getPreviousAdjacentEdge(bs.tags.edge6)
       ]
     }, %)

// define the tab plane
const tabPlane = {
  plane: {
    origin: { x: 0, y: 0, z: depth + thk },
    x_axis: { x: 1, y: 0, z: 0 },
    y_axis: { x: 0, y: 1, z: 0 },
    z_axis: { x: 0, y: 0, z: 1 }
  }
}

// build the tabs of the mounting bracket (right side)
const tabsR = startSketchOn(tabPlane)
  |> startProfileAt([width / 2 + thk, length / 2 + thk], %)
  |> line([tabWidth, -tabLength / 3], %, $edge11)
  |> line([0, -tabLength / 3 * 2], %, $edge12)
  |> line([-tabWidth, -tabLength / 3], %, $edge13)
  |> close(%, $edge14)
  |> hole(circle({
       center: [
         width / 2 + thk + tabWidth / 2,
         length / 2 + thk - (tabLength / (3 / 2))
       ],
       radius: holeDiam / 2
     }, %), %)
  |> extrude(-tabThk, %)
  |> fillet({
       radius: holeDiam / 2,
       tags: [
         getNextAdjacentEdge(edge12),
         getNextAdjacentEdge(edge11)
       ]
     }, %)
  |> patternLinear3d({
       axis: [0, -1, 0],
       instances: 2,
       distance: length + 2 * thk - (tabLength * 4 / 3)
     }, %)

// build the tabs of the mounting bracket (left side)
const tabsL = startSketchOn(tabPlane)
  |> startProfileAt([-width / 2 - thk, length / 2 + thk], %)
  |> line([-tabWidth, -tabLength / 3], %, $edge21)
  |> line([0, -tabLength / 3 * 2], %, $edge22)
  |> line([tabWidth, -tabLength / 3], %, $edge23)
  |> close(%, $edge24)
  |> hole(circle({
       center: [
         -width / 2 - thk - (tabWidth / 2),
         length / 2 + thk - (tabLength / (3 / 2))
       ],
       radius: holeDiam / 2
     }, %), %)
  |> extrude(-tabThk, %)
  |> fillet({
       radius: holeDiam / 2,
       tags: [
         getNextAdjacentEdge(edge21),
         getNextAdjacentEdge(edge22)
       ]
     }, %)
  |> patternLinear3d({
       axis: [0, -1, 0],
       instances: 2,
       distance: length + 2 * thk - (tabLength * 4 / 3)
     }, %)

// define a plane for retention bumps
const retPlane = {
  plane: {
    origin: { x: -width / 2 + 20, y: 0, z: 0 },
    x_axis: { x: 0, y: 1, z: 0 },
    y_axis: { x: 0, y: 0, z: 1 },
    z_axis: { x: 1, y: 0, z: 0 }
  }
}

// build the retention bump in the front
const retFront = startSketchOn(retPlane)
  |> startProfileAt([-length / 2 - thk, 0], %)
  |> line([0, thk], %)
  |> line([thk, -thk], %)
  |> close(%)
  |> extrude(width - 40, %)

// build the retention bump in the back
const retBack = startSketchOn(retPlane)
  |> startProfileAt([length / 2 + thk, 0], %)
  |> line([0, thk], %)
  |> line([-thk, 0], %)
  |> line([0, -thk], %)
  |> close(%)
  |> extrude(width - 40, %)
