fn make_circle = (face, tag, pos, radius) => {
  const sg = startSketchOn(face, tag)
  |> startProfileAt([pos[0] + radius, pos[1]], %)
  |> arc({
       angle_end: 360,
       angle_start: 0,
       radius: radius
     }, %, 'arc-' + tag)
  |> close(%)

  return sg
}

fn pentagon = (len) => {
  const sg = startSketchOn('XY')
  |> startProfileAt([-len / 2, -len / 2], %)
  |> angledLine({ angle: 0, length: len }, %, 'a')
  |> angledLine({
       angle: segAng('a', %) + 180 - 108,
       length: len
     }, %, 'b')
  |> angledLine({
       angle: segAng('b', %) + 180 - 108,
       length: len
     }, %, 'c')
  |> angledLine({
       angle: segAng('c', %) + 180 - 108,
       length: len
     }, %, 'd')
  |> angledLine({
       angle: segAng('d', %) + 180 - 108,
       length: len
     }, %)

  return sg
}

const p = pentagon(8)
  |> extrude(5, %)

const plumbus0 = make_circle(p, 'a', [0, 0], 1.5)
  |> extrude(3, %)
  |> fillet({
       radius: 0.5,
       tags: ['arc-a', getOppositeEdge('arc-a', %)]
     }, %)

const plumbus1 = make_circle(p, 'b', [0, 0], 1.5)
  |> extrude(3, %)
  |> fillet({
       radius: 0.5,
       tags: ['arc-b', getOppositeEdge('arc-b', %)]
     }, %)