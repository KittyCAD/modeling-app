fn cube = (length, center) => {
  let l = length/2
  let x = center[0]
  let y = center[1]
  let p0 = [-l + x, -l + y]
  let p1 = [-l + x,  l + y]
  let p2 = [ l + x,  l + y]
  let p3 = [ l + x, -l + y]

  return startSketchAt(p0)
  |> lineTo(p1, %)
  |> lineTo(p2, %)
  |> lineTo(p3, %)
  |> lineTo(p0, %)
  |> close(%)
  |> extrude(length, %)
}

let width = 20
fn transform = (i) => {
  // 10% bigger each time
  let bigger = pow(1.1, i)
  let smoler = pow(0.9, i)
  return {
    translate: [0, 0, -i * width],
    scale: [bigger, bigger, smoler],
    rotation: {
      angle: 90 * i,
      origin: "global",
    }
  }
}

let myCubes = 
  cube(width, [100,100])
  |> patternTransform(4, transform, %)
