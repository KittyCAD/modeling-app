cnr := "cargo nextest run"
cita := "cargo insta test --accept"

# Run the same lint checks we run in CI.
lint:
    cargo clippy --workspace --all-targets -- -D warnings

# Run the stdlib docs generation
redo-kcl-stdlib-docs-no-imgs:
    EXPECTORATE=overwrite {{cnr}} -p kcl-lib docs::gen_std_tests::test_generate_stdlib

# Generate the stdlib image artifacts
# Then run the stdlib docs generation
redo-kcl-stdlib-docs:
    TWENTY_TWENTY=overwrite {{cnr}} -p kcl-lib kcl_test_example
    EXPECTORATE=overwrite {{cnr}} -p kcl-lib docs::gen_std_tests::test_generate_stdlib


# Copy a test KCL file from executor tests into a new simulation test.
copy-exec-test-into-sim-test test_name:
    mkdir -p kcl/tests/{{test_name}}
    cp tests/executor/inputs/{{test_name}}.kcl kcl/tests/{{test_name}}/input.kcl
    zoo kcl fmt -w kcl/tests/{{test_name}}/input.kcl
    just new-sim-test {{test_name}}

# Create a new KCL deterministic simulation test case.
new-sim-test test_name render_to_png="true":
    # Add the various tests for this new test case.
    cat kcl/tests/simtest.tmpl | sed "s/TEST_NAME_HERE/{{test_name}}/" | sed "s/RENDER_TO_PNG/{{render_to_png}}/" >> kcl/src/simulation_tests.rs
    # Run all the tests for the first time, in the right order.
    {{cita}} -p kcl-lib -- simulation_tests::{{test_name}}::parse
    {{cita}} -p kcl-lib -- simulation_tests::{{test_name}}::unparse
    TWENTY_TWENTY=overwrite {{cita}} -p kcl-lib -- tests::{{test_name}}::kcl_test_execute

