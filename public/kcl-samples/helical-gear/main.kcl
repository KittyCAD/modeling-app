// Helical Gear
// A helical gear is a type of cylindrical gear where the teeth are slanted at an angle relative to the axis of rotation. This greatly reduces noise and wear when transmitting torque across meshed spinning gears

// Set units
@settings(defaultLengthUnit = mm)

// Define a function to create a helical gear
fn helicalGear(nTeeth, module, pressureAngle, helixAngle, gearHeight) {
  // Calculate gear parameters
  pitchDiameter = module * nTeeth
  addendum = module
  deddendum = 1.25 * module
  baseDiameter = pitchDiameter * cos(pressureAngle)
  tipDiameter = pitchDiameter + 2 * module

  // Define the constants of the keyway and the bore hole
  keywayWidth = 2
  keywayDepth = keywayWidth / 2
  holeDiam = 7
  holeRadius = holeDiam / 2
  startAngle = asin(keywayWidth / 2 / holeRadius)

  // Sketch the keyway and center hole
  holeWithKeyway = startSketchOn(XY)
    |> startProfile(at = [
         holeRadius * cos(startAngle),
         holeRadius * sin(startAngle)
       ])
    |> xLine(length = keywayDepth)
    |> yLine(length = -keywayWidth)
    |> xLine(length = -keywayDepth)
    |> arc(angleStart = -1 * startAngle + 360, angleEnd = 180, diameter = holeDiam)
    |> arc(angleStart = 180, angleEnd = startAngle, diameter = holeDiam)
    |> close()
    |> extrude(length = gearHeight)

  // Using the gear parameters, sketch an involute tooth spanning from the base diameter to the tip diameter
  helicalGearSketch = startSketchOn(XY)
    |> startProfile(at = polar(angle = 0, length = baseDiameter / 2))
    |> involuteCircular(
         startRadius = baseDiameter / 2,
         endRadius = tipDiameter / 2,
         angle = 0,
         tag = $seg01,
       )
    |> line(endAbsolute = polar(angle = 160 / nTeeth, length = tipDiameter / 2))
    |> involuteCircular(
         startRadius = baseDiameter / 2,
         endRadius = tipDiameter / 2,
         angle = -(4 * atan(segEndY(seg01) / segEndX(seg01))),
         reverse = true,
       )

    // Position the end line of the sketch at the start of the next tooth
    |> line(endAbsolute = polar(angle = 360 / nTeeth, length = baseDiameter / 2))

    // Pattern the sketch about the center by the specified number of teeth, then close the sketch
    |> patternCircular2d(%, instances = nTeeth, center = [0, 0])
    |> close()

  // Draw a gear sketch on the base plane
  return helicalGearSketch
    |> extrude(length = gearHeight, twistAngle = helixAngle)
    |> subtract([%], tools = [holeWithKeyway])
}

gearHeight = 7
helicalGear(
  nTeeth = 21,
  module = 2,
  pressureAngle = 20,
  helixAngle = 35,
  gearHeight,
)
