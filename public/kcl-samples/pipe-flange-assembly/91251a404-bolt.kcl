// 91251A404 Socket Head Cap Screw
// screw for mating the flanges together in the pipe flange assembly

// set units
@settings(defaultLengthUnit = in)


// import constants
import boltDiameter, boltLength, boltHeadLength, boltHeadDiameter, boltHexDrive, boltHexFlatLength, boltThreadLength from "globals.kcl"

// create a function to make a the bolt
export fn bolt() {
  // Create the head of the cap screw
  boltHead = startSketchOn(XZ)
    |> circle(center = [0, 0], radius = boltHeadDiameter / 2, tag = $topEdge)
    |> extrude(length = -boltHeadLength)
    |> fillet(radius = 0.020, tags = [topEdge, getOppositeEdge(topEdge)])

  // Define the sketch of the hex pattern on the screw head and extrude into the head
  hexPatternSketch = startSketchOn(boltHead, 'start')
    |> startProfileAt([
         boltHexDrive / 2,
         boltHexFlatLength / 2
       ], %)
    |> angledLine({
         angle = 270,
         length = boltHexFlatLength
       }, %)
    |> angledLine({
         angle = 210,
         length = boltHexFlatLength
       }, %)
    |> angledLine({
         angle = 150,
         length = boltHexFlatLength
       }, %)
    |> angledLine({
         angle = 90,
         length = boltHexFlatLength
       }, %)
    |> angledLine({
         angle = 30,
         length = boltHexFlatLength
       }, %)
    |> close()
    |> extrude(length = -boltHeadLength * 0.75)

  // create the body of the bolt
  boltBody = startSketchOn(boltHead, 'end')
    |> circle(center = [0, 0], radius = boltDiameter / 2, tag = $filletEdge)
    |> extrude(length = boltLength)
    |> appearance(color = "#4dd043", metalness = 90, roughness = 90)

  return boltBody
}

// https://www.mcmaster.com/91251a404/
