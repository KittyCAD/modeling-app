// Pipe and Flange Assembly
// A crucial component in various piping systems, designed to facilitate the connection, disconnection, and access to piping for inspection, cleaning, and modifications. This assembly combines pipes (long cylindrical conduits) with flanges (plate-like fittings) to create a secure yet detachable joint.

// set units
@settings(defaultLengthUnit = in)

// import constants
import * from "globals.kcl"

// import parts
import flange from "68095k348-flange.kcl"
import gasket from "9472k188-gasket.kcl"
import washer from "98017a257-washer.kcl"
import bolt from "91251a404-bolt.kcl"
import hexNut from "95479a127-hex-nut.kcl"
import pipe from "1120t74-pipe.kcl"

// place flanges
flange()
flange()
  |> rotate(axis = [0, 1, 0], angle = 180)
  |> translate(translate = [
       0,
       0,
       flangeBackHeight * 2 + gasketThickness
     ])

// place gasket between the flanges
gasket()
  |> translate(translate = [
       0,
       0,
       -flangeBackHeight - gasketThickness
     ])

// place eight washers (four front, four back)
washer()
  |> translate(translate = [
       mountingHolePlacementDiameter / 2,
       0,
       flangeBaseThickness
     ])
  |> patternCircular3d(
       %,
       instances = 4,
       axis = [0, 0, 1],
       center = [0, 0, 0],
       arcDegrees = 360,
       rotateDuplicates = false,
     )
  |> patternLinear3d(
       %,
       instances = 2,
       distance = -(flangeBaseThickness * 2 + flangeBackHeight * 2 + gasketThickness + washerThickness),
       axis = [0, 0, 1],
     )

// place four bolts
bolt()
  |> translate(translate = [
       mountingHolePlacementDiameter / 2,
       0,
       flangeBaseThickness + washerThickness
     ])
  |> rotate(roll = 90, pitch = 0, yaw = 0)
  |> patternCircular3d(
       %,
       instances = 4,
       axis = [0, 0, 1],
       center = [0, 0, 0],
       arcDegrees = 360,
       rotateDuplicates = false,
     )

// place four hex nuts
hexNut()
  |> translate(translate = [
       mountingHolePlacementDiameter / 2,
       0,
       -(flangeBackHeight * 2 + gasketThickness + flangeBaseThickness + washerThickness + hexNutThickness)
     ])
  |> patternCircular3d(
       %,
       instances = 4,
       axis = [0, 0, 1],
       center = [0, 0, 0],
       arcDegrees = 360,
       rotateDuplicates = false,
     )

// place both pieces of pipe
pipe()
  |> rotate(
       %,
       roll = -90,
       pitch = 0,
       yaw = 0,
     )
  |> translate(
       %,
       translate = [
         0,
         0,
         flangeBaseThickness + flangeFrontHeight - 0.5
       ],
       global = true,
     )

pipe()
  |> rotate(
       %,
       roll = 90,
       pitch = 0,
       yaw = 0,
     )
  |> translate(
       %,
       translate = [
         0,
         0,
         -(flangeBackHeight * 2 + gasketThickness + flangeBaseThickness + flangeFrontHeight - 0.5)
       ],
       global = true,
     )
