// Sheet Metal Bracket
// A component typically made from flat sheet metal through various manufacturing processes such as bending, punching, cutting, and forming. These brackets are used to support, attach, or mount other hardware components, often providing a structural or functional base for assembly.

// Set Units
@settings(defaultLengthUnit = in)

// Imput bolt pattern dimensions to mount the bracket
mountingBoltDiameter = 1 / 4
mountingBoltPatternX = 7
mountingBoltPatternY = 4

// Imput bolt pattern dimensions to mount a component to the bracket
componentBoltDiameter = 3 / 16
componentBoltPatternX = 1
componentBoltPatternY = 3

// Define bracket constants such as sheet metal thickness, bend radius, flange length, etc.
hatHeight = 2
bendAngle = 75
thickness = 0.125
interiorBendRadius = 0.09

// Calculate Remaining Parameters
exteriorBendRadius = interiorBendRadius + thickness
overhang = 3 * mountingBoltDiameter
flangeLength = 6 * mountingBoltDiameter
flangeExtrusion = if mountingBoltPatternY > componentBoltPatternY {
  mountingBoltPatternY + overhang * 2
} else {
  componentBoltPatternY + overhang * 2
}

// Draw the extrusion profile of the sheet metal bracket
sketch001 = startSketchOn(XZ)
  |> startProfileAt([
       -mountingBoltPatternX / 2 - overhang,
       0
     ], %)
  |> xLine(length = flangeLength)
  |> tangentialArc({
       radius = exteriorBendRadius,
       offset = bendAngle
     }, %)
  |> angledLineToY([bendAngle, hatHeight - thickness], %, $seg01)
  |> tangentialArc({
       radius = interiorBendRadius,
       offset = -bendAngle
     }, %)
  |> xLine(endAbsolute = 0, tag = $seg02)
  |> xLine(length = segLen(seg02))
  |> tangentialArc({
       radius = interiorBendRadius,
       offset = -bendAngle
     }, %)
  |> angledLine([-bendAngle, segLen(seg01)], %)
  |> tangentialArc({
       radius = exteriorBendRadius,
       offset = bendAngle
     }, %)
  |> xLine(length = flangeLength)
  |> yLine(length = thickness, tag = $seg03)
  |> xLine(length = -flangeLength, tag = $seg04)
  |> tangentialArc({
       radius = interiorBendRadius,
       offset = -bendAngle
     }, %)
  |> angledLine([180 - bendAngle, segLen(seg01)], %)
  |> tangentialArc({
       radius = exteriorBendRadius,
       offset = bendAngle
     }, %)
  |> xLine(endAbsolute = 0, tag = $seg05)
  |> xLine(length = -segLen(seg05))
  |> tangentialArc({
       radius = exteriorBendRadius,
       offset = bendAngle
     }, %)
  |> angledLine([bendAngle - 180, segLen(seg01)], %)
  |> tangentialArc({
       radius = interiorBendRadius,
       offset = -bendAngle
     }, %)
  |> xLine(length = -flangeLength, tag = $seg06)
  |> line(endAbsolute = [profileStartX(%), profileStartY(%)], tag = $seg07)
  |> close()
  |> extrude(length = flangeExtrusion)
  |> fillet(
       radius = flangeLength / 3,
       tags = [
         seg03,
         getOppositeEdge(seg03),
         seg07,
         getOppositeEdge(seg07)
       ],
     )

// Cut the bolt pattern in the left base flange
sketch002 = startSketchOn(sketch001, seg06)
profile001 = circle(
  sketch002,
  center = [
    mountingBoltPatternX / 2,
    overhang + mountingBoltPatternY
  ],
  radius = mountingBoltDiameter / 2,
)
profile002 = circle(sketch002, center = [mountingBoltPatternX / 2, overhang], radius = mountingBoltDiameter / 2)
extrude002 = extrude([profile001, profile002], length = -thickness)

// Cut the bolt pattern in the right base flange
sketch003 = startSketchOn(sketch001, seg04)
profile003 = circle(sketch003, center = [-mountingBoltPatternX / 2, overhang], radius = mountingBoltDiameter / 2)
profile004 = circle(
  sketch003,
  center = [
    -mountingBoltPatternX / 2,
    overhang + mountingBoltPatternY
  ],
  radius = mountingBoltDiameter / 2,
)
extrude003 = extrude([profile003, profile004], length = -thickness)

// Provision the top flange with holes to mount an object
sketch004 = startSketchOn(sketch001, seg05)
profile005 = circle(
  sketch004,
  center = [
    -componentBoltPatternX / 2,
    -componentBoltPatternY / 2 + flangeExtrusion / 2
  ],
  radius = componentBoltDiameter / 2,
)
profile006 = circle(
  sketch004,
  center = [
    -componentBoltPatternX / 2,
    componentBoltPatternY / 2 + flangeExtrusion / 2
  ],
  radius = componentBoltDiameter / 2,
)
profile007 = circle(
  sketch004,
  center = [
    componentBoltPatternX / 2,
    -componentBoltPatternY / 2 + flangeExtrusion / 2
  ],
  radius = componentBoltDiameter / 2,
)
profile008 = circle(
  sketch004,
  center = [
    componentBoltPatternX / 2,
    componentBoltPatternY / 2 + flangeExtrusion / 2
  ],
  radius = componentBoltDiameter / 2,
)
profile009 = circle(sketch004, center = [0, flangeExtrusion / 2], radius = .75)
extrude004 = extrude(
  [
    profile005,
    profile006,
    profile007,
    profile008,
    profile009
  ],
  length = -thickness,
)
