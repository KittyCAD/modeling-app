// Propeller
// A propeller is a rotating set of blades on a hub that converts shaft power into thrust by accelerating air or water rearward
// Categories: Aerospace

// Set units
@settings(defaultLengthUnit = in)

// Define parameters
bladeCount = 2
bladeDiameter = 86
hubDiameter = 8
hubThickness = 4
holeDiameter = .25
twistAngle = 45

// Model the central hub of the propeller
hub = startSketchOn(XY)
  |> circle(diameter = hubDiameter, tag = $seg01)
  // Create mounting holes around the perimeter of the hub
  |> subtract2d(tool =   circle(center = [0, hubDiameter / 3], diameter = holeDiameter)
    |> patternCircular2d(instances = 10, center = [0, 0]))
  |> extrude(
       length = hubThickness,
       symmetric = true,
       tagEnd = $capEnd001,
       tagStart = $capStart001,
     )
  |> fillet(
       tags = [
         getCommonEdge(faces = [seg01, capEnd001]),
         getCommonEdge(faces = [seg01, capStart001])
       ],
       radius = hubThickness / 6,
     )

// Bore a central hole from the hub. Chamfer the interior edges
hubBore = startSketchOn(hub, face = END)
  |> circle(diameter = hubDiameter / 4, tag = $seg02)
  |> extrude(length = -hubThickness)
  |> chamfer(
       tags = [
         getCommonEdge(faces = [capEnd001, seg02])
       ],
       length = hubThickness / 10,
     )

// Sketch the design space of each propeller blade, and revolve around the neutral axis
bladeRevolve = startSketchOn(YZ)
  |> startProfile(at = polar(angle = 40deg, length = hubDiameter / 2.01))
  |> arc(angleStart = 230, angleEnd = 280, radius = bladeDiameter / 50)
  |> tangentialArc(end = [
       bladeDiameter / 2.6,
       bladeDiameter / 30
     ])
  |> tangentialArc(endAbsolute = [bladeDiameter / 2, 0.01])
  |> yLine(endAbsolute = 0)
  |> xLine(endAbsolute = profileStartX())
  |> yLine(endAbsolute = profileStartY())
  |> close()
  |> revolve(axis = X)
  // Subtract out a mounting face to the center hub
  |> subtract(tools =   startSketchOn(XY)
    |> circle(diameter = hubDiameter)
    |> extrude(length = hubThickness * 5, symmetric = true))

// Sketch the cross-section of the blade. Extrude with a twist down the length of the blade
bladeTwist = startSketchOn(-XZ)
  |> startProfile(at = [bladeDiameter / 10, 0])
  |> arc(angleStart = 0, angleEnd = 83, radius = 0.05)
  |> tangentialArc(endAbsolute = [-lastSegX(), lastSegY()])
  |> tangentialArc(angle = 83deg, radius = 0.05)
  |> mirror2d(axis = X)
  |> close()
  |> extrude(twistAngle, length = bladeDiameter / 1.9)

// Model the blades by intersecting the outer profile to the twisted section. Pattern about the shaft axis
blades = intersect([bladeRevolve, bladeTwist])
  |> patternCircular3d(instances = bladeCount, axis = Z, center = [0, 0, 0])

// Apply wood appearance to the entire propeller model
[hub, blades]
  |> appearance(color = "#8b5a2b")
