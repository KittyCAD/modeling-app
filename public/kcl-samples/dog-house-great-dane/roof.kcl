// Dog House (Great Dane) - Roof
// Roof rafters and slats for the dog house.

@settings(experimentalFeatures = allow, defaultLengthUnit = in)

import * from "dimensions.kcl"
import * from "2x4.kcl"

// Side slat spacing (match wall cadence for rafters)
sideSlatWidth = studDepth
sideSlatGap = 2
sideSlatPitch = sideSlatWidth + sideSlatGap
sideSlatStartY = studDepth
sideSlatEndY = frameLength - studDepth - sideSlatWidth
sideSlatSpanY = sideSlatEndY - sideSlatStartY
sideSlatCount = floor(sideSlatSpanY / sideSlatPitch) + 1
sideSlatActualPitch = sideSlatSpanY / (sideSlatCount - 1)

// Roof framing
roofHalfWidth = frameWidth / 2
roofAngle = atan(roofRise / roofHalfWidth)
rafterLength = sqrt(roofHalfWidth * roofHalfWidth + roofRise * roofRise)
rafterThickness = studThickness
rafterWidth = studDepth
rafterMidZ = (peakZ + eaveZ) / 2
ridgeBoardZ = peakZ - studDepth

rafterStartY = sideSlatStartY + (sideSlatWidth - rafterWidth) / 2
rafterEndY = frameLength - studDepth - rafterWidth
rafterSpanY = rafterEndY - rafterStartY
rafterCount = sideSlatCount
rafterSpacing = sideSlatActualPitch

// Collar ties (brace rafters together to reduce ridge load)
collarTieHeightFactor = 0.5
collarTieCenterZ = eaveZ + roofRise * collarTieHeightFactor
collarTieHalfWidth = roofHalfWidth * (peakZ - collarTieCenterZ) / (peakZ - eaveZ) - (studThickness / 2)
collarTieLength = 2 * collarTieHalfWidth
collarTieMinZ = collarTieCenterZ - (studThickness / 2)

// Import upper side wall beams as reference geometry
topRightPlate = twoByFourY(
  minX = frameWidth / 2 - studDepth,
  minY = 0,
  minZ = studTopZ,
  lengthY = frameLength,
)

topLeftPlate = twoByFourY(
  minX = -frameWidth / 2,
  minY = 0,
  minZ = studTopZ,
  lengthY = frameLength,
)

// Create half-width rafter beams to make slot cuts
rightRafterhalf = twoByFourX(
       minX = -rafterLength / 2 - 3,
       minY = rafterStartY,
       minZ = -rafterThickness / 2,
       lengthX = rafterLength + 3,
     )
  |> rotate(axis = [0, 1, 0], angle = -roofAngle)
  |> translate(x = roofHalfWidth / 2, y = 3.5 / 2, z = rafterMidZ)
  |> scale(x = 1.025, z = 1.025, global = false)

leftRafterHalf = twoByFourX(
       minX = -rafterLength / 2,
       minY = rafterStartY,
       minZ = -rafterThickness / 2,
       lengthX = rafterLength + 3,
     )
  |> rotate(axis = [0, 1, 0], angle = roofAngle)
  |> translate(x = -roofHalfWidth / 2, y = -3.5 / 2, z = rafterMidZ)
  |> scale(x = 1.025, z = 1.025, global = false)

// Model the left and right rafters. Subtract interface to mesh with other beams
rightRafter = twoByFourX(
       minX = -rafterLength / 2 - 3,
       minY = rafterStartY,
       minZ = -rafterThickness / 2,
       lengthX = rafterLength + 3,
     )
  |> rotate(axis = [0, 1, 0], angle = -roofAngle)
  |> translate(x = roofHalfWidth / 2, y = 0, z = rafterMidZ)
  |> subtract(tools = leftRafterHalf)
  |> subtract(tools = topLeftPlate)

leftRafter = twoByFourX(
       minX = -rafterLength / 2,
       minY = rafterStartY,
       minZ = -rafterThickness / 2,
       lengthX = rafterLength + 3,
     )
  |> rotate(axis = [0, 1, 0], angle = roofAngle)
  |> translate(x = -roofHalfWidth / 2, y = 0, z = rafterMidZ)
  |> subtract(tools = rightRafterhalf)
  |> subtract(tools = topRightPlate)

// Create the center collar tie
collarTie = twoByFourX(
       minX = -collarTieHalfWidth + 2.25,
       minY = rafterStartY,
       minZ = collarTieMinZ + 0.5,
       lengthX = collarTieLength - 4.5,
     )
  |> subtract(tools = [clone(leftRafter), clone(rightRafter)])

// Pattern the roof beams the length of the roof
rightRafters = patternLinear3d(
  [rightRafter],
  axis = [0, 1, 0],
  distance = rafterSpacing,
  instances = rafterCount,
)

leftRafters = patternLinear3d(
  [leftRafter],
  axis = [0, 1, 0],
  distance = rafterSpacing,
  instances = rafterCount,
)

collarTies = patternLinear3d(
  [collarTie],
  axis = [0, 1, 0],
  distance = rafterSpacing,
  instances = rafterCount,
)

// Model a ridgeboard to hold it all together
ridgeBoardSlots = startSketchOn(YZ)
  |> startProfile(at = [
       rafterStartY - 0.025,
       ridgeBoardZ + 3.75
     ])
  |> yLine(length = -3.16)
  |> xLine(length = 3.55)
  |> yLine(length = 3.18)
  |> line(endAbsolute = [profileStartX(%), profileStartY(%)])
  |> close()
  |> extrude(length = 5, symmetric = true)
  |> patternLinear3d(instances = rafterCount, distance = rafterSpacing, axis = [0, 1, 0])

ridgeBoard = twoByFourYRotated(
       minX = -studThickness / 2,
       minY = 0,
       minZ = ridgeBoardZ + 1,
       lengthY = frameLength,
     )
  |> subtract(tools = ridgeBoardSlots)

// Apply wooden appearance
[
       rightRafters,
       leftRafters,
       collarTies,
       ridgeBoard
     ]
  |> appearance(color = woodColor, roughness = woodRoughness, metalness = woodMetalness)
