// Helical Planetary Gearset
// A helical planetary gearset is a type of planetary gear system where the teeth of the sun gear, planet gears, and/or ring gear are helical rather than straight. This design allows for smoother, quieter operation, greater load-carrying capacity, and more flexible shaft alignment.
// Categories: Automotive, Aerospace

// Set units
@settings(defaultLengthUnit = mm)

// Define a function to create a helical gear
fn helicalGear(nTeeth, module, pressureAngle, helixAngle, gearHeight) {
  // Calculate gear parameters
  pitchDiameter = module * nTeeth
  addendum = module
  deddendum = 1.25 * module
  baseDiameter = pitchDiameter * cos(pressureAngle)
  tipDiameter = pitchDiameter + 2 * module

  // Define the constants of the keyway and the bore hole
  keywayWidth = 1
  keywayDepth = keywayWidth / 2
  holeDiam = 7
  holeRadius = holeDiam / 2
  startAngle = asin(keywayWidth / 2 / holeRadius)

  // Sketch the keyway and center hole
  holeWithKeyway = startSketchOn(XY)
    |> startProfile(at = [
         holeRadius * cos(startAngle),
         holeRadius * sin(startAngle)
       ])
    |> xLine(length = keywayDepth)
    |> yLine(length = -keywayWidth)
    |> xLine(length = -keywayDepth)
    |> arc(angleStart = -1 * startAngle + 360deg, angleEnd = 180deg, radius = holeRadius)
    |> arc(angleStart = 180deg, angleEnd = startAngle, radius = holeRadius)
    |> close()

  // Using the gear parameters, sketch an involute tooth spanning from the base diameter to the tip diameter
  helicalGear = startSketchOn(XY)
    |> startProfile(at = polar(angle = 0, length = baseDiameter / 2))
    |> involuteCircular(
         startDiameter = baseDiameter,
         endDiameter = tipDiameter,
         angle = 0,
         tag = $seg01,
       )
    |> line(endAbsolute = polar(angle = 160deg / nTeeth, length = tipDiameter / 2))
    |> involuteCircular(
         startDiameter = baseDiameter,
         endDiameter = tipDiameter,
         angle = -(4 * atan(segEndY(seg01) / segEndX(seg01))),
         reverse = true,
       )

    // Position the end line of the sketch at the start of the next tooth
    |> line(endAbsolute = polar(angle = 360deg / nTeeth, length = baseDiameter / 2))

    // Pattern the sketch about the center by the specified number of teeth, then close the sketch
    |> patternCircular2d(
         instances = nTeeth,
         center = [0, 0],
         arcDegrees = 360deg,
         rotateDuplicates = true,
       )
    |> close()

    // Compute twist angle so the tooth helix matches helixAngle over the gear height
    |> extrude(length = gearHeight, twistAngle = 360 * gearHeight * tan(helixAngle) / (3.14 * pitchDiameter), twistAngleStep = 0)
    |> subtract(tools =     holeWithKeyway
      |> extrude(length = gearHeight))

  return helicalGear
}

// Define a function to create a ring gear
fn ringGear(nTeeth, module, pressureAngle, helixAngle, gearHeight) {
  // Calculate gear parameters
  pitchDiameter = module * nTeeth
  addendum = module
  deddendum = 1.25 * module
  baseDiameter = pitchDiameter * cos(pressureAngle)
  tipDiameter = pitchDiameter + 2 * module

  // Using the gear parameters, sketch an involute tooth spanning from the base diameter to the tip diameter
  ringGear = startSketchOn(XY)
    |> circle(radius = tipDiameter / 1.85)
    |> subtract2d(tool =     startSketchOn(XY)
      |> startProfile(at = polar(angle = 0, length = baseDiameter / 2))
      |> involuteCircular(
           startRadius = baseDiameter / 2,
           endRadius = tipDiameter / 2,
           angle = 0,
           tag = $seg01,
         )
      |> line(endAbsolute = polar(angle = 200deg / nTeeth, length = tipDiameter / 2))
      |> involuteCircular(
           startRadius = baseDiameter / 2,
           endRadius = tipDiameter / 2,
           angle = -(4 * atan(segEndY(seg01) / segEndX(seg01))),
           reverse = true,
         )

      // Position the end line of the sketch at the start of the next tooth
      |> line(endAbsolute = polar(angle = 360 / nTeeth, length = baseDiameter / 2))

      // Pattern the sketch about the center by the specified number of teeth, then close the sketch
      |> patternCircular2d(
           instances = nTeeth,
           center = [0, 0],
           arcDegrees = 360deg,
           rotateDuplicates = true,
         )
      |> close())

    // Create a circular body that is larger than the tip diameter of the gear, then subtract the gear profile from the body
    |> extrude(length = gearHeight, twistAngle = 360 * gearHeight * tan(helixAngle) / (3.14 * pitchDiameter), twistAngleStep = 0)

  return ringGear
}

// Create the outer ring gear for the planetary gearset
ringGear(
  nTeeth = 40,
  module = 1.5,
  pressureAngle = 14deg,
  helixAngle = -25deg,
  gearHeight = 5,
)

// Create a central sun gear using a small helical gear
helicalGear(
       nTeeth = 12,
       module = 1.5,
       pressureAngle = 14deg,
       helixAngle = 25deg,
       gearHeight = 5,
     )
  |> rotate(yaw = -4)

// Create the helical planet gears
numPlanetGears = 4
helicalGear(
       nTeeth = 12,
       module = 1.5,
       pressureAngle = 14deg,
       helixAngle = -25deg,
       gearHeight = 5,
     )
  |> translate(y = (12 + 12) / 2 * 1.5 + 2)
  |> patternCircular3d(
       instances = numPlanetGears,
       axis = [0, 0, 1],
       center = [0, 0, 0],
       arcDegrees = 360deg,
       rotateDuplicates = false,
     )
