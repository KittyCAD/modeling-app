// T-Slotted Framing Rail
// A T-slotted framing rail, or T-slot extrusion, is a rectangular or square aluminum profile with a "T" shaped slot along one or more sides. These slots allow for easy attachment of various hardware components like brackets, connectors, and fasteners, making it a versatile and customizable framing system.

// Set units
@settings(defaultLengthUnit = in, kclVersion = 1.0)

fn railTslot(railHeight, railLength) {
  // Sketch one inner leg of the extruded rail
  railProfile = startSketchOn(XZ)
    |> startProfile(at = [
         railHeight / 2,
         (1 - 0.356) / 2 * railHeight
       ])
    |> xLine(length = -0.12)
    |> tangentialArc(angle = 45, radius = .09 * railHeight)
    |> angledLine(angle = 45, endAbsoluteY = 0.113 * railHeight)
    |> tangentialArc(angle = 135, radius = .015 * railHeight)
    |> xLine(endAbsolute = (.5 - (.320 / 2) - .015) * railHeight)
    |> tangentialArc(angle = -90, radius = .015 * railHeight)
    |> yLine(endAbsolute = 0.015 * railHeight)
    |> tangentialArc(angle = -90, radius = .015 * railHeight)
    |> xLine(length = -0.03 * railHeight)
    |> arc(angleStart = 0, angleEnd = 180, radius = 0.072 / 4 * railHeight)
    |> xLine(length = -0.1 * railHeight)
    |> arc(angleStart = 0, angleEnd = 180, radius = 0.072 / 4 * railHeight)
    |> xLine(length = -0.03 * railHeight)
    |> tangentialArc(endAbsolute = [.04, .04])

    // Mirror the sketch about the diagonal to complete the leg. Then mirror across the center of the profile in the horizontal and vertical directions. Then close the sketch
    |> mirror2d(axis = {
         direction = [1.0, 1.0],
         origin = [0.0, 0.0]
       })
    |> mirror2d(axis = {
         direction = [1.0, 0.0],
         origin = [0.0, 0.5 * railHeight]
       })
    |> mirror2d(axis = {
         direction = [0.0, 1.0],
         origin = [0.5 * railHeight, 0.0]
       })
    |> close()
    // Sketch a dimensioned hole in the center of the profile
    |> subtract2d(tool = circle(center = [.5 * railHeight, .5 * railHeight], radius = .205 * railHeight / 2))
    |> extrude(length = -railLength)
  return railProfile
}

// Generate one rail using the rail function
railTslot(railHeight = 1.5, railLength = 2ft)
