// Brake Rotor
// A 320mm vented brake disc (rotor), with straight vanes, 30mm thick. The disc bell should accommodate 5 M12 wheel studs on a 114.3mm pitch circle diameter.

// Define units
@settings(defaultLengthUnit = in)

// Import parameters
import * from "parameters.kcl"

// Sketch and revolve the rotor bell geometry
sketchDiscBell = startSketchOn(-XY)
  |> startProfile(at = [rotorBore / 2, 0])
  |> yLine(length = -tBell)
  |> xLine(length = lugSpacing - rotorBore + tBell)
  |> angledLine(angle = rDraftAngle, lengthY = hBellAboveDiscFace, tag = $seg08)
  |> tangentialArc(angle = -90deg - rDraftAngle, diameter = wUndercut, tag = $seg06)
  |> angledLine(angle = tangentToEnd(seg06) + turns::HALF_TURN, length = tDiscHalf, tag = $seg07)
  |> xLine(length = -1 * (tBell + wUndercut), tag = $seg04)
  |> angledLine(angle = segAng(seg08) + 180deg, endAbsoluteY = profileStartY())
  |> line(endAbsolute = [profileStartX(%), profileStartY(%)], tag = $seg09)
  |> close()
  |> revolve(axis = Y)

// Remove lug holes from the bell face
lugHoles = startSketchOn(sketchDiscBell, face = seg09)
  |> circle(center = [-lugSpacing / 2, 0], diameter = lugThreadDiameter)
  |> patternCircular2d(instances = lugCount, center = [0, 0], arcDegrees = 360deg)
  |> extrude(length = -tBell * 2)

// Model the vents within the brake rotor discs
ventPad = startSketchOn(sketchDiscBell, face = seg04)
  |> startProfile(at = [rVentFillet, rotorDiameter / 2])
  |> xLine(length = wVent - (2 * rVentFillet))
  |> tangentialArc(angle = -90deg, radius = rVentFillet)
  |> yLine(length = -hFrictionSurface + 2 * rVentFillet, tag = $seg10)
  |> tangentialArc(angle = -90deg, radius = rVentFillet)
  |> xLine(endAbsolute = profileStartX())
  |> tangentialArc(angle = -90deg, radius = rVentFillet)
  |> yLine(length = segLen(seg10))
  |> tangentialArc(endAbsolute = [profileStartX(%), profileStartY(%)])
  |> close()
  |> extrude(length = tVent, method = NEW)
ventSet = patternCircular3d(
  ventPad,
  instances = nVentBosses,
  axis = Y,
  center = [0, 0, 0],
)

// Drill a hole pattern on each friction disc
rInner = rotorDiameter / 2 - (hFrictionSurface / 1.2)
rOuter = rotorDiameter / 2

// Adjusted base angle
aBaseAdj = aBase + 180deg / nVentBosses

// Three ring points (t = 0.2, 0.5, 0.8) using adjusted base
r1 = rInner + 0.2 * (rOuter - rInner)
a1 = aBaseAdj - (0.2 * aSweep)
x1 = r1 * cos(a1)
y1 = r1 * sin(a1)

r2 = rInner + 0.5 * (rOuter - rInner)
a2 = aBaseAdj - (0.5 * aSweep)
x2 = r2 * cos(a2)
y2 = r2 * sin(a2)

r3 = rInner + 0.8 * (rOuter - rInner)
a3 = aBaseAdj - (0.8 * aSweep)
x3 = r3 * cos(a3)
y3 = r3 * sin(a3)

// Inboard disc half (sketch on START face of ventPad)
faceIn = startSketchOn(ventPad, face = START)
ringIn = circle(faceIn, radius = rotorDiameter / 2)
  |> subtract2d(tool = circle(faceIn, radius = segEndX(seg07)))
h1In = circle(faceIn, center = [x1, y1], radius = dDrillDia / 2)
h2In = circle(faceIn, center = [x2, y2], radius = dDrillDia / 2)
h3In = circle(faceIn, center = [x3, y3], radius = dDrillDia / 2)
holesIn = patternCircular2d(
  [h1In, h2In, h3In],
  instances = nArcs,
  center = [0, 0],
  arcDegrees = 360deg,
)
profileIn = subtract2d(ringIn, tool = holesIn)
discInboard = extrude(profileIn, length = tDiscHalf, method = NEW)

// Outboard disc half created by linear pattern along Y gap
discOutboard = patternLinear3d(
  discInboard,
  axis = [0, 1, 0],
  distance = tVent + tDiscHalf,
  instances = 2,
)

// Add appearance
rotor = appearance(
  [sketchDiscBell, ventSet, discOutboard],
  color = "#f0d77a",
  metalness = 70,
  roughness = 30,
)
