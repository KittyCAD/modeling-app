// Car Wheel
// A sports car wheel with a circular lug pattern and spokes.

// Define units
@settings(defaultLengthUnit = in)

// Import parameters
import * from "parameters.kcl"

// Create the cutout for each mounting lug
lugMounts = startSketchOn(XY)
  |> startProfile(at = [lugSpacing / 2, 0])
  |> xLine(length = lugThreadDiameter / 2)
  |> yLine(length = -tBell - 0.85)
  |> xLine(endAbsolute = profileStartX() + lugDiameter / 2)
  |> yLine(length = -lugLength * 2)
  |> xLine(endAbsolute = profileStartX())
  |> yLine(endAbsolute = profileStartY(), tag = $seg01)
  |> close()
  |> revolve(axis = seg01)

// Draw a profile for the center of the wheel, and revolve
wheelCenter = startSketchOn(XY)
  |> startProfile(at = [rotorBore / 2, -tBell])
  |> yLine(length = -0.75)
  |> xLine(endAbsolute = 0)
  |> yLine(length = -0.5)
  |> arc(angleStart = 270deg, angleEnd = 290deg, radius = lugSpacing / 1.15)
  |> xLine(endAbsolute = lugSpacing / 2.15)
  |> tangentialArc(angle = -50deg, radius = lugSpacing / 7)
  |> tangentialArc(angle = 30deg, radius = lugSpacing / 7)
  |> angledLine(angle = 90deg - (90 / spokeCount), endAbsoluteY = profileStartY(), tag = $seg02)
  |> xLine(endAbsolute = profileStartX())
  |> close()
  |> revolve(axis = Y)
  |> subtract(tools =   lugMounts
    |> patternCircular3d(instances = lugCount, axis = [0, -1, 0], center = [0, 0, 0]))

// Define and revolve the wheel exterior. Adjust position based on wheel parameters and offset
rim = startSketchOn(XY)
  |> startProfile(at = [
       rimDiameter / 2,
       segEndY(seg02) - (wheelWidth / 2) + offset
     ])
  |> yLine(length = wheelWidth * 0.15, tag = $seg04)
  |> angledLine(angle = 135deg, lengthY = wheelWidth * 0.025)
  |> yLine(length = wheelWidth * 0.25)
  |> angledLine(angle = 45deg, lengthY = wheelWidth * 0.025)
  |> yLine(length = wheelWidth * 0.55, tag = $seg03)
  |> line(end = [wheelWidth * 0.05, wheelWidth * .01])
  |> yLine(length = wheelWidth * 0.025)
  |> xLine(length = -wheelWidth * 0.02)
  |> yLine(length = -wheelWidth * 0.01)
  |> line(end = [-wheelWidth * 0.05, -wheelWidth * 0.01])
  |> yLine(length = -segLen(seg03))
  |> angledLine(angle = 45deg, lengthY = -wheelWidth * 0.025)
  |> yLine(length = -wheelWidth * 0.1)
  |> angledLine(angle = 200deg, lengthX = wheelWidth * 0.07, tag = $seg08)
  |> yLine(length = -.2, tag = $seg06)
  |> angledLine(angle = -45deg, lengthX = wheelWidth * 0.07, tag = $seg05)
  |> yLine(length = -wheelWidth * 0.065)
  |> angledLine(angle = 135deg, lengthY = -wheelWidth * 0.025)
  |> angledLine(angle = -90deg, length = segLen(seg04))
  |> line(end = [wheelWidth * 0.05, -wheelWidth * 0.01])
  |> yLine(length = -wheelWidth * 0.01)
  |> xLine(length = wheelWidth * 0.02)
  |> yLine(length = wheelWidth * 0.025)
  |> line(endAbsolute = [profileStartX(), profileStartY()])
  |> close()
  |> revolve(axis = Y)

// Profile the side view of a wheel spoke
spokeSide = startSketchOn(XY)
  |> startProfile(at = segStart(seg02))
  |> line(endAbsolute = segEnd(seg02))
  |> angledLine(angle = -32deg, length = 0.1)
  |> tangentialArc(endAbsolute = segStart(seg06), tag = $seg09)
  |> line(endAbsolute = segEnd(seg06))
  |> line(endAbsolute = segEnd(seg05))
  |> angledLine(angle = tangentToEnd(seg09) + 5, length = -0.01)
  |> tangentialArc(endAbsolute = [profileStartX(), profileStartY()])
  |> close()

// Profile the face view of a wheel spoke
spokeFace = startSketchOn(XZ)
  |> startProfile(at = [-lugSpacing / 1.25, 0])
  |> arc(
       angleStart = 0,
       angleEnd = 90 - (170 / spokeCount),
       radius = lugSpacing / spokeCount,
       tag = $seg07,
     )
  |> angledLine(angle = tangentToEnd(seg07), length = rimDiameter / 2)
  |> tangentialArc(angle = -180deg, diameter = 0.25)
  |> line(endAbsolute = polar(angle = 180 - (140 / spokeCount), length = segEndX(seg02)))
  |> tangentialArc(endAbsolute = polar(angle = 180 - (180 / spokeCount), length = segStartX(seg02) + 0.01))
  |> arc(interiorAbsolute = polar(angle = 180 - (180 / spokeCount / 2), length = segStartX(seg02)), endAbsolute = [-segStartX(seg02), 0])
  // Mirror the spoke on it's center axis
  |> mirror2d(axis = X)
  |> close()

// Create a spoke by intersecting the side and face profiles, then pattern around the center of the wheel
spokes = intersect([
       spokeFace
  |> extrude(length = wheelWidth * 2),
       spokeSide
  |> rotate(pitch = 90)
  |> revolve(axis = Y)
     ])
  |> patternCircular3d(instances = spokeCount, axis = Y, center = [0, 0, 0])

wheel = appearance(
  [wheelCenter, spokes, rim],
  color = "#ededed",
  metalness = 70,
  roughness = 30,
)
